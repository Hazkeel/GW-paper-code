{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e678d3be",
   "metadata": {},
   "source": [
    "## Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "68b6d58c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Packages Used\n",
    "\n",
    "#------------------------To solve the ODE----------------------------\n",
    "using DifferentialEquations, LSODA\n",
    "\n",
    "#------------------------Writing Data to CSV file--------------------\n",
    "using CSV, Tables\n",
    "\n",
    "# #-----------------------For Benchmarking the ODE solver------------------\n",
    "# using BenchmarkTools\n",
    "# using Logging: global_logger\n",
    "# using TerminalLoggers: TerminalLogger\n",
    "# global_logger(TerminalLogger())   "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9a26bdbf",
   "metadata": {},
   "source": [
    "## Defining the System"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b972371d",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "#---------------------------System Parameters-----------------------------------\n",
    "\n",
    "m1 = 4.8e-11\n",
    "m2 = 4.8e-14\n",
    "D  = 1e5 \n",
    "a0 = 2.88e-8                 # 100*r_isco\n",
    "e0 = 0.\n",
    "\n",
    "#---------------------------Mass Array------------------------------------------\n",
    "\n",
    "m_total = m1+m2\n",
    "mu = (m1*m2)/(m1+m2)\n",
    "mass=[m1,m2,m_total,mu]\n",
    "\n",
    "#---------------------------Initial Conditions----------------------------------\n",
    "\n",
    "function initial_conditions(mass, a0, e0, phi0 = 0.)\n",
    "    \"\"\"\n",
    "    Calculate the initial conditions for a Keplerian orbit with parameters a, e\n",
    "    \"\"\"\n",
    "    r0 = a0 * (1. - e0^2) / (1. + e0 * cos(phi0))\n",
    "    dphi0 = sqrt(mass[3] * a0 * (1. - e0^2)) / r0^2\n",
    "    dr0 = a0* (1. - e0^2) / (1 + e0*cos(phi0))^2 * e0 *sin(phi0)*dphi0\n",
    "    return [r0, phi0, dr0, dphi0]\n",
    "end\n",
    "\n",
    "u0 = initial_conditions(mass, a0, e0)\n",
    "\n",
    "#-------------------------Halo Models--------------------------------------------\n",
    "\n",
    "function Spike_Halo(r, rho_spike, r_spike, alpha, r_min=0.)   # Spike Model\n",
    "    \n",
    "    \"\"\"\n",
    "    Defining the Halo Model from Eda paper [https://arxiv.org/abs/1408.3534]\n",
    "    \n",
    "    The density is given by:\n",
    "        rho (r) = rho_spike*(r_spike/r)^(alpha)\n",
    "    \n",
    "    Parameters:\n",
    "        rho_spike : float\n",
    "            The density parameter of the spike profile\n",
    "        r_spike : float\n",
    "            The scale radius of the spike profile\n",
    "        alpha : float\n",
    "            The power-law index of the spike profile, with condition 0 < alpha < 3\n",
    "    \"\"\"\n",
    "    \n",
    "    return  ifelse.(r .> r_min, rho_spike.*(r_spike./r).^alpha, 0.)\n",
    "end\n",
    "\n",
    "\n",
    "function Const_Halo(r, rho_0, r_min=0.)    # Constant Density Model\n",
    "    \n",
    "    \"\"\"\n",
    "    Constant Halo with density rho_0\n",
    "    \"\"\"\n",
    "    \n",
    "    return  ifelse.(r .> r_min, rho_0, 0.)\n",
    "end\n",
    "\n",
    "\n",
    "#--------------------Halo Parameters from Eda paper----------------------------------\n",
    "\n",
    "rho_spike = 1.0848e-11               # [226.*ms.solar_mass_to_pc]\n",
    "alpha_spike = 7/2.8\n",
    "r_spike = 0.54;\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ec2b5ca8",
   "metadata": {},
   "source": [
    "## Evolving the System"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5f015c9f",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "#-------------------------Evolution Parameters--------------------------------------------\n",
    "\n",
    "F0 = sqrt(mass[3]/a0^3)/2/pi\n",
    "nOrbits = 1000                     # Number of Orbits\n",
    "tp_per_orb = 100                   # Time-points per orbit\n",
    "dp = nOrbits*tp_per_orb            # total number of data points the code runs for\n",
    "t_end = nOrbits/F0\n",
    "\n",
    "\n",
    "#-------------------------Evolution Function-----------------------------------------------\n",
    "\n",
    "function Evolve(mass, u0, t_end, t_start=0., gwEmission=true, dynamicalFriction=true, PostNewtonian=true, coulombLog=3., nSteps=1000)\n",
    "    \n",
    "    \"\"\"\n",
    "    Evolve the system of differential equations from t_start to t_end with initial conditions u0\n",
    "    \"\"\"\n",
    "    t_eval = LinRange(t_start, t_end, nSteps)\n",
    "    eta = mass[4]/mass[3]\n",
    "    m = mass\n",
    "    tspan = (t_start,t_end)\n",
    " \n",
    "    \n",
    "    #---------------------------Derivative Function---------------------------------------\n",
    "    \n",
    "    function orbit!(du, u, m, t)\n",
    "        \n",
    "        # r=u[1],  phi=u[2],  dr=u[3],  dphi=u[4]\n",
    "        # m = [m1,m2,m_total,mu]\n",
    "        \n",
    "        v = sqrt(u[3]^2 + (u[1]^2)*(u[4])^2) \n",
    "        \n",
    "        P_df     = dynamicalFriction ? 4*pi*(m[2]^2 / m[4]) * (Spike_Halo(u[1],rho_spike, r_spike, alpha_spike)) * (coulombLog/v^3) : 0.\n",
    "        \n",
    "        P_pn_r   = PostNewtonian ? (m[3]/u[1]^2)*( (4 + 2*eta)*(m[3]/u[1]) - (3*eta +1)*(u[1]^2)*(u[4]^2) + (3 - 7*eta/2)*u[3]^2) : 0.\n",
    "        \n",
    "        P_pn_phi = PostNewtonian ? (m[3]/u[1]^2)*(4 - 2*eta)*u[3]*u[4] : 0.\n",
    "        \n",
    "        P_gw_r   = gwEmission ? ( (8/5)*m[4]*(m[3]/u[1]^3)*(2*v^2 + (8/3)*(m[3]/u[1]) )) : 0.\n",
    "        \n",
    "        P_gw_phi = gwEmission ? ( (8/5)*m[4]*(m[3]/u[1]^3)*(v^2 + 3*(m[3]/u[1]) )) : 0.\n",
    "        \n",
    "        \n",
    "        du[1] = u[3]  # dr\n",
    "        du[2] = u[4]  # dphi\n",
    "        du[3] = -(-P_gw_r + P_df)*(u[3]) + P_pn_r - (m[3]/u[1]^2) + u[1]*(u[4]^2)  # ddr\n",
    "        du[4] = -(P_gw_phi + P_df)*(u[4]) + P_pn_phi - (2*u[3]*u[4]/u[1])          # ddphi    \n",
    "    end\n",
    "    \n",
    "    \n",
    "    #---------------------Call-back functions---------------------------------------------\n",
    "    \n",
    "    function terminate_condition(u,t,integrator)       # condition at which the integration terminates\n",
    "        u[1]< 2.0e-9\n",
    "    end\n",
    "    \n",
    "    function terminate_affect!(integrator)\n",
    "        terminate!(integrator)\n",
    "    end\n",
    "    \n",
    "    terminate_cb =DiscreteCallback(terminate_condition,terminate_affect!)\n",
    " \n",
    "    \n",
    "    #-------------------------Calling the solver--------------------------------------------\n",
    "    \n",
    "    # call-backs do not work with lsoda() but they do work with other algorithms\n",
    "    \n",
    "    prob = ODEProblem(orbit!, u0, tspan, m) #, callback=terminate_cb)  # exclude the callback part when using lsoda()\n",
    "    alg =  lsoda() #Vern9(lazy=false) #Feagin12() #Vern7() #lsoda()\n",
    "    @time sol = solve(prob, alg, abstol=1e-14, reltol=1e-12, saveat=t_eval, dense=false, maxiters=Int(1e9)) \n",
    "    return sol\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8c09ad99",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "solution = Evolve(mass,u0,t_end,0.,true,true,true,3., dp)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c3d2613c",
   "metadata": {},
   "source": [
    "## Writing the Orbit data to a CSV file\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "44f8ff9e",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "#----------------------------------------------------------------------------\n",
    "sol_transpose = solution'\n",
    "t = solution.t\n",
    "\n",
    "A = hcat(sol_transpose[:,:],t[:])     # Stacking the matrices together\n",
    "\n",
    "#-----------------------------------------------------------------------------\n",
    "CSV.write(\"PN_GW_DF_Orb1k_dp100_alpha_2point50.csv\", Tables.table(A), writeheader=false)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
